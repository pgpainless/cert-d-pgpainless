// SPDX-FileCopyrightText: 2022 Paul Schaub <vanitasvitae@fsfe.org>
//
// SPDX-License-Identifier: Apache-2.0

plugins {
    id 'application'
}

group 'org.pgpainless'

repositories {
    mavenCentral()
}

dependencies {
    // Junit
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    // Logging
    testImplementation "ch.qos.logback:logback-classic:$logbackVersion"

    // pgp.cert.d using PGPainless
    implementation project(":pgpainless-cert-d")

    // SQL subkey table
    implementation "org.pgpainless:pgp-cert-d-java-jdbc-sqlite-lookup:$pgpCertDJavaVersion"

    // picocli for cli
    implementation "info.picocli:picocli:$picocliVersion"
}

test {
    useJUnitPlatform()
}

mainClassName = 'pgp.cert_d.cli.PGPCertDCli'

jar {
    dependsOn(":pgpainless-cert-d:assemble")
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

